apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'eclipse-wtp'

repositories {
	jcenter()
	mavenCentral()

	maven {
		// for jasper reports
		url 'http://jaspersoft.jfrog.io/jaspersoft/third-party-ce-artifacts/'
	}

	if (project.hasProperty('iPLAssMavenRepoUrl')) {
		maven {
			url iPLAssMavenRepoUrl
		}
	}
}

dependencies {
	implementation platform("org.iplass:iplass-bom:${iPLAssVersion}")

	implementation 'org.iplass:iplass-core'
	implementation 'org.iplass:iplass-gem'
	implementation 'org.iplass:iplass-web'

	runtimeOnly 'org.iplass:iplass-admin'
	runtimeOnly 'org.iplass:iplass-tools'

	providedRuntime group: 'org.iplass', name: 'iplass-tools-batch', version: iPLAssVersion, transitive: false

	// Infinispanを利用する場合は以下のコメントアウトを外してください。
//	runtimeOnly 'org.iplass:iplass-infinispan'

	// Redisを利用する場合は以下のコメントアウトを外してください。
//	runtimeOnly 'org.iplass:iplass-redis'

	//provided JavaEE7 apis
	//Servlet
	providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
	//JSP
	compileOnly 'javax.servlet.jsp:javax.servlet.jsp-api:2.3.2-b02'
	//Expression Language
	compileOnly 'javax.el:javax.el-api:3.0.1-b04'
	//jstl
	compileOnly 'javax.servlet.jsp.jstl:javax.servlet.jsp.jstl-api:1.2.1'
	//for jstl tld file resolve
	compileOnly group: 'org.glassfish.web', name: 'javax.servlet.jsp.jstl', version: '1.2.3', transitive : false
	//JAX-RS
	compileOnly 'javax.ws.rs:javax.ws.rs-api:2.0.1'
	//Common Annotations for the Java Platform
	compileOnly 'javax.annotation:javax.annotation-api:1.2'
	//java mail
	compileOnly 'javax.mail:javax.mail-api:1.5.6'
	//Bean validation
	compileOnly group: 'javax.validation', name: 'validation-api', version: '1.1.0.Final'

	//javax activation for java11
	compileOnly group: 'jakarta.activation', name: 'jakarta.activation-api', version: '1.2.1'

	//JAXB for java11
	compileOnly group: 'jakarta.xml.bind', name: 'jakarta.xml.bind-api', version: '2.3.2'

	//if your app server is tomcat 8.5, use this
	//jstl runtime
	runtimeOnly 'javax.servlet.jsp.jstl:javax.servlet.jsp.jstl-api:1.2.1'
	runtimeOnly(group: 'org.glassfish.web', name: 'javax.servlet.jsp.jstl', version: '1.2.3') {
		//use javax.servlet.jsp.jstl:javax.servlet.jsp.jstl-api
		exclude(group: 'javax.servlet.jsp.jstl', module: 'jstl-api')
	}

	//java mail runtime
	runtimeOnly(group: 'com.sun.mail', name: 'javax.mail', version: '1.5.6') {
		//use com.sun.activation:jakarta.activation
		exclude(group: 'javax.activation', module: 'activation')
	}

	//Bean validation runtime
	runtimeOnly group: 'javax.validation', name: 'validation-api', version: '1.1.0.Final'
	runtimeOnly group: 'org.hibernate', name: 'hibernate-validator', version: '5.4.1.Final'
	runtimeOnly group: 'org.glassfish', name: 'javax.el', version: '3.0.1-b08'

	//JAX-RS runtime
	runtimeOnly group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet', version: '2.25.1'
	runtimeOnly group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version: '2.25.1'
	runtimeOnly group: 'org.glassfish.jersey.media', name: 'jersey-media-multipart', version: '2.25.1'
	runtimeOnly group: 'com.fasterxml.jackson.jaxrs', name: 'jackson-jaxrs-json-provider', version: '2.8.9'

	//JAXB runtime for java11
	runtimeOnly group: 'jakarta.xml.bind', name: 'jakarta.xml.bind-api', version: '2.3.2'
	runtimeOnly group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: '2.3.2'

	//JAF runtime for java11
	runtimeOnly group: 'com.sun.activation', name: 'jakarta.activation', version: '1.2.1'

	testImplementation 'junit:junit:4.12'

	testImplementation 'org.iplass:iplass-test'

	// 以下のJDBCドライバはテナント作成ツール(runTenantBatchタスク)の実行に必要となります。
	// 実行する場合は使用するJDBCドライバのコメントアウトを外してください。
	// OracleのJDBCドライバについては'lib/jdbc'に配置してください。
//	providedRuntime 'mysql:mysql-connector-java:8.0.18'
//	providedRuntime 'org.postgresql:postgresql:42.2.8'
//	providedRuntime 'com.microsoft.sqlserver:mssql-jdbc:7.4.1.jre8'

	providedRuntime fileTree('lib').include('**/*.jar')
}

tasks.withType(AbstractCompile) {
	options.encoding = 'UTF-8'
}

tasks.withType(GroovyCompile) {
	options.encoding = 'UTF-8'
}

tasks.withType(JavaExec) {
	if (System.getProperty('mtp.config') != null) {
		systemProperty 'mtp.config', System.getProperty('mtp.config')
	}
}

// テナント作成ツール実行
task runTenantBatch(type: JavaExec, group: 'application', description : 'execute tenant batch.') {
	main = "org.iplass.mtp.tools.batch.tenant.TenantBatch"
	classpath = sourceSets.main.runtimeClasspath
}

// Entity Java マッピングクラス作成ツール実行
task createEntityClass(type: JavaExec, group: 'application', description : 'create entity java class.') {
	main = "org.iplass.mtp.tools.batch.entity.EntityJavaMappingClassCreator"
	classpath = sourceSets.main.runtimeClasspath
	standardInput = System.in
}

// マージ済service-config表示ツール実行
//   引数：
//     -m [モード]   : モード(PARSE_ONLY:パースのみ、PARSE_LOAD:パース及びサービスのロード)、未指定時はPARSE_ONLY
//     -o [ファイル] : 出力ファイル名、未指定時はコンソール出力
//   実行例１：パースのみ、コンソール出力
//     gradlew viewMergedServiceConfig
//   実行例２：パース及びサービスのロード、'skeleton-service-config.xml'ファイルへ出力
//     gradlew viewMergedServiceConfig --args="-m PARSE_LOAD -o skeleton-service-config.xml"
task viewMergedServiceConfig(type: JavaExec, group: 'application', description : 'view merged service-config.') {
	main = "org.iplass.mtp.tools.batch.config.ServiceConfigViewer"
	classpath = sourceSets.main.runtimeClasspath
}
